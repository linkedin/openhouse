plugins {
  id 'com.github.johnrengelman.shadow' version '7.1.2'
  id 'openhouse.tables-test-fixtures-common'
  id 'openhouse.iceberg-conventions-1.5.2'
}

import com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer

ext {
  sparkVersion = '3.5.2'
}

project.evaluationDependsOn(':tables-test-fixtures:tables-test-fixtures_2.12')

sourceSets {
  main {
    java {
      srcDirs += project(':tables-test-fixtures:tables-test-fixtures_2.12').sourceSets.main.java.srcDirs
    }
    resources {
      srcDirs += project(':tables-test-fixtures:tables-test-fixtures_2.12').sourceSets.main.resources.srcDirs
    }
  }
  test {
    java {
      srcDirs += project(':tables-test-fixtures:tables-test-fixtures_2.12').sourceSets.test.java.srcDirs
    }
  }
}

dependencies {
  compileOnly('org.apache.spark:spark-sql_2.12:' + sparkVersion){
    // These classes are available from `client-codegen-convention.gradle`
    exclude group: "io.netty"
  }
}

shadowJar {
  transform(PropertiesFileTransformer) {
    paths = ['META-INF/spring.factories']
    mergeStrategy = "merge"
  }
}
